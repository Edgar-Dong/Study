apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'//greendao
apply plugin: 'CheckResourcePrefixPlugin'//checkresourceconflict
//apply plugin: 'kotlin-kapt'//for kotlin,java not use this
apply plugin: 'dagger.hilt.android.plugin'

def config = rootProject.ext.android
def appId = rootProject.ext.appId

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        applicationId appId["app"]
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName
        flavorDimensions "versionCode"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        //arouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        //databinding
        dataBinding{
            enabled true
        }

        //multidex
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //resourcePrefix资源前缀限定，只能限定布局文件名和value资源的key，并不能限定图片资源的文件名
    //给电商工程加上前缀约束shopping_
    //resourcePrefix "app_"

    productFlavors {
        baidu {
            applicationId "com.android.example"
            versionCode 1
            versionName "1.0"
        }
        xiaomi {
            applicationId "com.android.example"
            versionCode 1
            versionName "1.0"
        }
        huawei {
            applicationId "com.android.example"
            versionCode 2
            versionName "2.0"
        }
    }
}

//greendao
greendao {
    schemaVersion 1
}

checkResourceConfig {
    // 是否开启插件
    enabled false
    // 运行完后自动使用默认浏览器打开html结果进行预览
    autoPreviewResult true
}

def library = rootProject.ext.dependencies

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation library.appcompat
    implementation library.constraintlayout
    implementation library.design
    testImplementation library.junit
    androidTestImplementation library.ext_junit
    androidTestImplementation library.espresso_core

    implementation 'org.greenrobot:greendao:3.3.0'//greendao
    implementation 'com.airbnb.android:lottie:3.4.4'//三方动画库lottie
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'//数据库在线调试

    implementation library.arouter_api//arouter
    annotationProcessor library.arouter_compiler//arouter

    if (isModule) {
        implementation project(path: ':component_module1')
        implementation project(path: ':component_module2')
    } else {
        implementation project(path: ':common')
    }

    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //hilt begin
    implementation "com.google.dagger:hilt-android:2.28-alpha"
    //kapt "com.google.dagger:hilt-android-compiler:2.28-alpha"//for kotlin
    annotationProcessor "com.google.dagger:hilt-android-compiler:2.28-alpha"//for java
    //hilt end

    implementation("com.squareup.okhttp3:okhttp:4.9.0")
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //jetpack
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //navigation
    implementation 'androidx.navigation:navigation-fragment:2.3.2'
    implementation 'androidx.navigation:navigation-ui:2.3.2'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //multidex
    implementation 'com.android.support:multidex:1.0.1'

    implementation 'org.java-websocket:Java-WebSocket:1.4.0'

//    // debugImplementation because LeakCanary should only run in debug builds.
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def date = new Date().format("yyyy_MM_dd_HHmmss", TimeZone.getTimeZone("GMT+08"))
        if (variant.buildType.name.equals('debug')) {
            //Study_debug_${versionName}_${channel}_${date}.apk
            outputFileName = "study_release_${versionName}_${variant.productFlavors[0].name}_${date}.apk"
        }
        if (variant.buildType.name.equals('release')) {
            //Study_release_${versionName}_${channel}_${date}.apk
            outputFileName = "study_release_${versionName}_${variant.productFlavors[0].name}_${date}.apk"
        }
    }
}